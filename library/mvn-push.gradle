apply plugin: 'maven'
apply plugin: 'signing'

def isReleaseBuild() {
    return android.defaultConfig.versionName.contains("SNAPSHOT") == false
}

def sonatypeRepositoryUrl() {
    if (isReleaseBuild()) {
        return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
    } else {
        return hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL : "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

def getRepositoryUsername() {
    return hasProperty('nexusUsername') ? nexusUsername : ""
}

def getRepositoryPassword() {
    return hasProperty('nexusPassword') ? nexusPassword : ""
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.groupId = 'jp.co.recruit_mp'
                pom.artifactId = 'LightCalendarView'
                pom.version = android.defaultConfig.versionName

                repository(url: sonatypeRepositoryUrl()) {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }

                pom.project {
                    name 'LightCalendarView'
                    packaging 'aar'
                    description 'A lightweight monthly calendar view for Android, fully written in Kotlin. Designed to meet the minimum demands for typical calendars.'
                    url 'https://github.com/recruit-mp/light-calendarview'

                    scm {
                        url 'https://github.com/recruit-mp/light-calendarview.git'
                        connection 'scm:git@github.com:recruit-mp/light-calendarview.git'
                        developerConnection 'scm:git@github.com:recruit-mp/light-calendarview.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id "recruit_mp"
                            name 'Recruit Marketing PartnersCo.,Ltd.'
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.getSrcDirs()
    }

    artifacts {
        archives androidSourcesJar
    }
}
